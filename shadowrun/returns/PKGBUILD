# Maintainer: Hildigerr Vergaray <Maintainer at YmirSystems dot com>

pkgname=(gog-shadowrun-returns gog-shadowrun-returns-editor)
_pkgname=shadowrun-returns
pkgver=2.0.0.7
pkgrel=3
pkgdesc="Shadowrun Returns (GOG version)"
arch=('x86_64')
url="https://www.gog.com/en/game/shadowrun_returns"
license=('custom')
depends=(
  lib32-glu
  lib32-libxcursor
  lib32-alsa-plugins
  lib32-libpulse
  lib32-mesa
  xdg-launcher
)
options=(!strip)
source=(
  "gog_shadowrun_returns_${pkgver}.sh"
  "shadowrun-returns.sh"
  "shadowrun.returns.desktop"
)
sha256sums=(
  '94030db95ee681224033bce3cc00d7ea61b10ff00a93e1d29c2459a35bd45121'
  '42a5f075fb5c623493546f07dd0a8fb6d7b29549b5d9b5a0fafcc347e4ab4d7a'
  '37a6de999ff394893673296f0bb2a037d2c45053b0f95362f7880aaeab6249c2'
)

build() {
  tail -n 12 "${srcdir}/data/noarch/game/Shadowrun.sh" >> "${srcdir}shadowrun-returns.sh"
}

package_gog-shadowrun-returns() {
  optdepends=('gog-shadowrun-returns-editor: to launch content editor')
  cd "${srcdir}/data/noarch"
  install -Dm644 "docs/End User License Agreement.txt" "${pkgdir}/usr/share/licenses/gog/${_pkgname}.eula"
  install -Dm644 "support/icon.png" "${pkgdir}/usr/share/icons/hicolor/256x256/apps/${_pkgname}.png"
  cd game
  install -Dm755 Shadowrun "${pkgdir}/opt/shadowrun/returns/Shadowrun"
  find Shadowrun_Data -type f -exec install -Dm644 "{}" "${pkgdir}/opt/shadowrun/returns/{}" \;
  install -Dm755 "${srcdir}/shadowrun-returns.sh" "${pkgdir}/usr/bin/shadowrun-returns"
  install -Dm644 "${srcdir}/shadowrun.returns.desktop" "${pkgdir}/usr/share/applications/com.gog.shadowrun.returns.desktop"
}

package_gog-shadowrun-returns-editor() {
  depends=(
    'gog-shadowrun-returns'
    'lib32-fontconfig'
    'lib32-libsm'
    'lib32-libxt'
    'lib32-qt4'
  )
  install -Dm755 "${srcdir}/data/noarch/game/ShadowrunEditor" "${pkgdir}/opt/shadowrun/returns/ShadowrunEditor"
}

